plugins {
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
}

ext {
	guavaVer = '31.1-jre'
}

repositories {
	mavenCentral()
}

configure(subprojects) {
	group = 'com.fitdine'
	version = '0.0.1-SNAPSHOT'

	apply plugin: 'java'
	apply plugin: 'project-report'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	def javaVer = '21'
	targetCompatibility = javaVer
	sourceCompatibility = javaVer
	compileJava.options.encoding = "UTF-8"

	repositories {
		mavenCentral()
	}

	dependencies {
		implementation 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.3"
		}
	}

	test {
		useJUnitPlatform{
			excludeTags 'integration-test'
		}
	}

	task integrationTest(type: Test) {
		maxHeapSize = "4g"
		useJUnitPlatform() {
			includeTags 'integration-test'
		}
		shouldRunAfter test
	}
}

task buildAll(type: GradleBuild) {
	tasks = ['clean', 'build', 'integrationTest']
}
